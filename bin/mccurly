#!/usr/bin/perl

use strict;
use warnings;

use File::Path qw(make_path);
use File::Basename qw(dirname);

# globals
my $fmt = $ENV{MCCURLY_FN_PATH_FMT} || "data/%s/functions/%s.mcfunction";
my $pp = $ENV{MCCURLY_PREPROCESSOR} || "";
my $ext = $ENV{MCCURLY_STRIP_EXT} || "\\.mccurly";

# loop through each filename
foreach my $infile (@ARGV) {
	# open file with preprocessor if necessary
	my $in;
	if ($pp eq "") {
		open($in, "<", $infile) or die "$!";
	} else {
		open($in, "-|", "$pp '$infile'") or die "$!";
	}

	# open output file
	my @fhstack = ();
	my $outfile = "$infile.out";
	$outfile =~ s/$ext\.out$//;
	print "$outfile\n";
	open(my $out, ">", $outfile) or die "$!";
	push @fhstack, $out;

	# read input lines
	while (my $line = <$in>) {
		# figure out what to do
		chomp $line;
		my $op = "";
		if ($line =~ /function ([^:]+):([^ :]+).*{\s*$/) {
			$outfile = sprintf "$fmt", $1, $2;
			$op = "push";
		} elsif ($line =~ /}\s*$/) {
			$op = "pop";
		}

		# adjust line
		$line =~ s/\s*[{}]\s*$//g;
		$line =~ s/^\s*//g;
		$line =~ s/\s+/ /g if not $line =~ /^#/;
		print { $fhstack[-1] } "$line\n";

		# add/remove handle to/from fhstack if necessary
		if ($op eq "push") {
			# open file handle and add it to the stack
			-e dirname($outfile) or make_path(dirname($outfile)) or die "$!";
			print "$outfile\n";
			open(my $fh, ">", $outfile) or die "$!";
			push @fhstack, $fh;
		} elsif ($op eq "pop") {
			pop @fhstack;
		}
	}
}

