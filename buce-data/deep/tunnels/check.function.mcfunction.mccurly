#!/usr/bin/env sempl
SEMPL_EXPERIMENTAL=1
#!end sempl env
# runs every tick while a player wears a tunnel helmet in a minecart

# revoke advancement
advancement revoke @s only buce:deep/tunnels/trigger

{\\}
{%for axis in north_south east_west; do}{\\}
{%	if [[ $axis == north_south ]]; then }{\\}
{%		facing='~ ~ ~1' }{\\}
{%		coord_index=2 }{\\}
{%	else }{\\}
{%		facing='~1 ~ ~' }{\\}
{%		coord_index=0 }{\\}
{%	fi }{\\}
# generate the tunnel
execute on vehicle at @s if block ~ ~ ~ #minecraft:rails[shape={$axis}] facing {$facing} run function buce:deep/tunnels/{$axis} {
	# runs as a minecart building a tunnel

	## debug
	#tellraw @a ["Pos: ", {"nbt": "Pos", "entity": "@s"}]

	# let var = coord % 6
	#
	# side view w/ value of var as cart moves across rail:
	#
	#
	#    |     |     |     <- pillars
	# -------------------  <- rail
	# 0123450123450123450  <- var
	#
	execute store result score @s var run data get entity @s Pos[{$coord_index}]
	#tellraw @a ["var0 : ",{"score":{"name":"@s","objective":"var"}}]
	scoreboard players operation @s var %= #buce.tunnel.length var
	#tellraw @a ["var1 : ",{"score":{"name":"@s","objective":"var"}}]

	## set facing along rail and figure out which tunnel to build
	#execute on vehicle at @s facing {$facing} if @s[predicate=buce:deep/tunnels/above] run function buce:deep/tunnels/above
	#execute on vehicle at @s facing {$facing} if @s[predicate=buce:deep/tunnels/below] run function buce:deep/tunnels/below
	#execute on vehicle at @s facing {$facing} if dimension minecraft:overworld @s[predicate=!buce:deep/tunnels/above,predicate=!buce:deep/tunnels/below] run function buce:deep/tunnels/middle
	#execute on vehicle at @s facing {$facing} if dimension minecraft:the_end run function buce:deep/tunnels/end
	execute if dimension minecraft:the_nether run function buce:deep/tunnels/nether/build {
		# runs as a minecart in the nether

		# place sculk
		execute positioned ^ ^ ^3 run function buce:deep/tunnels/sculk
		execute positioned ^ ^ ^-3 run function buce:deep/tunnels/sculk {
			## debug
			#say sculk

			# runs as a minecart building a tunnel
			setblock ^2 ^3 ^ minecraft:sculk keep
			setblock ^-2 ^3 ^ minecraft:sculk keep
			setblock ^2 ^-1 ^ minecraft:sculk keep
			setblock ^-2 ^-1 ^ minecraft:sculk keep
		}

		# place blocks against bottom sculk lines
		execute if score @s var matches 1 positioned ^ ^ ^2 run function buce:deep/tunnels/nether/bottom
		execute if score @s var matches 5 positioned ^ ^ ^-2 run function buce:deep/tunnels/nether/bottom {
			## debug
			#say bottom

			# runs as a minecart building a tunnel
			setblock ^2 ^ ^ minecraft:polished_blackstone keep
			setblock ^-2 ^ ^ minecraft:polished_blackstone keep
			setblock ^1 ^-1 ^ minecraft:polished_blackstone_brick_slab keep
			setblock ^-1 ^-1 ^ minecraft:polished_blackstone_brick_slab keep
		}

		# place blocks against top sculk lines
		execute if score @s var matches 2 positioned ^ ^ ^1 run function buce:deep/tunnels/nether/top
		execute if score @s var matches 4 positioned ^ ^ ^-1 run function buce:deep/tunnels/nether/top {
			## debug
			#say top

			# runs as a minecart building a tunnel
			setblock ^2 ^2 ^ minecraft:polished_blackstone keep
			setblock ^-2 ^2 ^ minecraft:polished_blackstone keep
			setblock ^1 ^3 ^ minecraft:polished_blackstone keep
			setblock ^-1 ^3 ^ minecraft:polished_blackstone keep
		}

		# fill in middle gaps
		execute if score @s var matches 3 run function buce:deep/tunnels/nether/middle {
			## debug
			#say middle

			# runs as a minecart building a tunnel
			setblock ^2 ^1 ^ minecraft:chiseled_polished_blackstone keep
			setblock ^-2 ^1 ^ minecraft:chiseled_polished_blackstone keep
			setblock ^ ^3 ^ minecraft:polished_blackstone keep
		}
	}
}

{%done}{\\}

